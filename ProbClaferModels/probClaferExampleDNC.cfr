// probClaferExample4.cfr
// Author: Jordan A. Ross
// Date: 1/26/2015
// Last updated: 3/5/2015 
//
// NOTE:: This model does not compile, it is used as a medium to show how new constructs will
// be used in Clafer. 
//
// See README.md for example description


abstract Rational
    numerator  ->> integer
    denominator ->> integer

abstract Probability : Rational
    [numerator <= denominator]
    [numerator >= 0 && denominator > 0]
    complement : Rational
        [numerator = parent.denominator.ref - parent.numerator.ref]
        [denominator = parent.denominator.ref]

// Function inspiration by Andrzej for scaling rational numbers
abstract GCD (x :Rational) :integer
    [x.numerator % this = 0] 
    [x.denominator % this = 0]
    [this >= 1]
    [all k :integer | k > this => x.numerator % k != 0 || x.denominator % k != 0]

abstract reduce (x :Rational) :Rational
    [ numerator = x.numerator / GCD x ]
    [ denominator = x.denominator / GCD x ]

enum FailureClassification = Omission | Comission

abstract Failure : Probability
    class -> FailureClassification

abstract BasicEvent : Failure


abstract FailureExpression : Failure
    operand -> Failure *
    [one operand.class]
    [this.class = operand.class]


abstract AND : FailureExpression
    [numerator = product operand.numerator]
    [denominator = product operand.denominator]

abstract OR : FailureExpression
    // Create a reference to the set of operands and compute the complement of each probability
    // in the set.
    myOp -> Probability * = operand
        complement : Rational
            [numerator = parent.denominator.ref - parent.numerator.ref] // need the refs here
            [denominator = parent.denominator.ref] // need the refs here

    // Get the product of the complements probability
    complProductProb : Probability
    [complProductProb.numerator = product operand.complement.numerator]
    [complProductProb.denominator = product operand.complement.denominator]

    [numerator = product complProductProb.complement.numerator]
    [denominator = product complProductProb.complement.denominator]

abstract NOP : FailureExpression
    [one operand]
    [numerator  = operand.numerator.ref]
    [denominator = operand.denominator.ref]
    

abstract ComponentFailure : Failure
    exp : FailureExpression
    // C: The failure classificaiton for this clafer is the same as for the exp
    [exp.class = this.class]
    [this.numerator = exp.numerator.ref]
    [this.denominator = exp.denominator.ref]



abstract PortFailure : Failure
    exp : FailureExpression
    // C: The failure classificaiton for this clafer is the same as for the exp
    [exp.class = this.class]
    [this.numerator = exp.numerator.ref]
    [this.denominator = exp.denominator.ref]


abstract System
    abstract Connection
        source -> Port
        sink -> Port
        [sink.failure.ref = source.failure.ref]
    
    abstract Component
        abstract Port
            failure -> PortFailure ?
        abstract OutPort : Port
        abstract InPort : Port
        
        inputPorts -> InPort *
        [inputPorts = this.InPort]
        outputPorts -> OutPort *
        [outputPorts = this.OutPort]

    abstract SourceComponent : Component
    abstract SinkComponent : Component

    connections -> Connection *
    [connections = this.Connection]      

    sourceComponents -> SourceComponent *
    [sourceComponents = this.SourceComponent]
    sinkComponents -> SinkComponent *
    [sinkComponents = this.SinkComponent]
    components -> Component *
    [components = this.Component]

    
// Concrete clafer for represent "no failure possible"
NoFailure : Failure
    [numerator = 0 && denominator = 1]


SimpleSystem : System
    compX : SourceComponent
        dataOut1 : OutPort
            [failure = compX.pFailure]
        dataOut2 : OutPort
            [failure = compX.pFailure]
        dataOut3 : OutPort
            [failure = compX.pFailure]

        pFailure : PortFailure
            // We redefine 'exp' in PortFailure to be a specific FailureExpression, namely NOP
            exp : NOP
            [exp.operand = NoFailure]
    compA : Component
        dataOut : OutPort
            [failure = compA.pFailure]
        dataIn : InPort

        // Basic events
        internalFailure : BasicEvent
            [numerator = 1 && denominator = 10]
            [class = Omission]

        // Port failures. This will define a failure that we can link to a port
        pFailure : PortFailure
            // We redefine 'exp' in PortFailure to be a specific FailureExpression, namely NOP
            exp : NOP
            [exp.operand = compA.internalFailure]

    compB : Component
        dataOut : OutPort
            [failure = compB.pFailure]
        dataIn : InPort

        // Basic events
        internalFailure : BasicEvent
            [numerator = 1 && denominator = 10]
            [class = Omission]

        // Port failures. This will define a failure that we can link to a port
        pFailure : PortFailure
            exp : NOP
            [exp.operand= compB.internalFailure]
    compC : Component
        dataOut : OutPort
            [failure = compC.pFailure]
        dataIn : InPort

        // Basic events
        internalFailure : BasicEvent
            [numerator = 1 && denominator = 10]
            [class = Omission]

        // Port failures. This will define a failure that we can link to a port
        pFailure : PortFailure
            exp : NOP
            [exp.operand = compC.internalFailure]
    compD : Component
        dataOut : OutPort
            [failure = compD.pFailure]
        dataIn : InPort

        // Basic events
        internalFailure : BasicEvent
            [numerator = 2 && denominator = 10]
            [class = Omission]

        // Port failures. This will define a failure that we can link to a port
        pFailure : PortFailure
            exp : OR 
            [exp.operand = compD.dataIn.failure, compD.internalFailure]
    compY : SinkComponent
        dataIn1 : InPort
        dataIn2 : InPort
        dataIn3 : InPort
        
        cFailure : ComponentFailure
            exp : AND
            [exp.operand= (compY.dataIn1.failure, compY.dataIn2.failure, compY.dataIn3.failure)]


    // Now lets define some connections
    conX_A : Connection
        [source = compX.dataOut1]
        [sink = compA.dataIn]
    conX_B : Connection
        [source = compX.dataOut2]
        [sink = compB.dataIn]
    conX_C : Connection
        [source = compX.dataOut3]
        [sink = compC.dataIn]
    conA_Y : Connection
        [source = compA.dataOut]
        [sink = compY.dataIn1]
    conB_Y : Connection
        [source = compB.dataOut]
        [sink = compY.dataIn2]
    conD_Y : Connection
        [source = compD.dataOut]
        [sink = compY.dataIn3]
    conC_D : Connection
        [source = compC.dataOut]
        [sink = compD.dataIn]

    
